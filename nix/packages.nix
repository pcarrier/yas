{ inputs, ... }:
{
  perSystem =
    { system, ... }:
    let
      common = import ./common.nix { inherit inputs system; };
      inherit (common)
        pkgs
        version
        cargoLockConfig
        rustToolchain
        rustPlatform
        craneLib
        craneLibStatic
        src
        commonArgs
        commonArgsStatic
        cargoArtifacts
        cargoArtifactsStatic
        ;
      serverVaapiEnabled = pkgs.stdenv.isLinux;
      bindgenClangArgs = pkgs.lib.optionalString pkgs.stdenv.isLinux "-isystem ${pkgs.lib.getDev pkgs.stdenv.cc.libc}/include";

      # Runtime library search path for blit-server's dlopen GPU backends.
      #   pkgs.libva           → libva.so.2, libva-drm.so.2
      #   addDriverRunpath     → /run/opengl-driver  (libcuda, libnvidia-encode,
      #                          Mesa VA-API drivers, etc.)
      gpuRuntimeLibPath = pkgs.lib.optionalString serverVaapiEnabled (
        pkgs.lib.makeLibraryPath [
          pkgs.libva
          pkgs.addDriverRunpath.driverLink
        ]
      );

      # ------------------------------------------------------------------
      # Crane workspace build (every workspace crate compiles once)
      # ------------------------------------------------------------------

      blit-workspace = craneLib.buildPackage (
        commonArgs
        // {
          pname = "blit-workspace";
          inherit cargoArtifacts;
          cargoExtraArgs = "--workspace --exclude blit-browser";
          doCheck = false;
          preBuild = copyWebAppDist;
          postInstall = ''
            $out/bin/blit generate $out/share
          '';
        }
      );

      # Split individual binaries out of the workspace build.
      mkBin =
        {
          pname,
          binName ? pname,
          mainProgram ? binName,
          withMan ? true,
          withCompletions ? false,
        }:
        pkgs.runCommand "${pname}-${version}" { meta = { inherit mainProgram; }; } (
          ''
            mkdir -p $out/bin
            cp ${blit-workspace}/bin/${binName} $out/bin/
          ''
          + pkgs.lib.optionalString withMan ''
            if [ -d "${blit-workspace}/share/man" ]; then
              mkdir -p $out/share
              ln -s ${blit-workspace}/share/man $out/share/man
            fi
          ''
          + pkgs.lib.optionalString withCompletions ''
            for dir in fish bash-completion zsh; do
              if [ -d "${blit-workspace}/share/$dir" ]; then
                mkdir -p $out/share/$dir
                cp -r ${blit-workspace}/share/$dir/* $out/share/$dir/
              fi
            done
          ''
        );

      blit-server = mkBin { pname = "blit-server"; };
      blit-cli = mkBin {
        pname = "blit-cli";
        binName = "blit";
        withCompletions = true;
      };
      blit-gateway = mkBin { pname = "blit-gateway"; };
      blit-proxy = mkBin { pname = "blit-proxy"; };
      blit-webrtc-forwarder = mkBin {
        pname = "blit-webrtc-forwarder";
      };

      # ------------------------------------------------------------------
      # WASM (still uses wasm-pack, not crane)
      # ------------------------------------------------------------------

      browserWasm = rustPlatform.buildRustPackage {
        pname = "blit-browser";
        inherit version;
        src = ../.;
        cargoBuildFlags = [
          "-p"
          "blit-browser"
        ];
        cargoLock = cargoLockConfig;
        nativeBuildInputs = [
          pkgs.wasm-pack
          pkgs.wasm-bindgen-cli
          pkgs.binaryen
        ];
        buildPhase = ''
          cd crates/browser
          HOME=$TMPDIR wasm-pack build --target web --release --out-dir $out
        '';
        dontInstall = true;
        doCheck = false;
      };

      # ------------------------------------------------------------------
      # Static binaries (musl on Linux, for release tarballs)
      # ------------------------------------------------------------------

      blit-workspace-static = craneLibStatic.buildPackage (
        commonArgsStatic
        // {
          pname = "blit-workspace-static";
          cargoArtifacts = cargoArtifactsStatic;
          cargoExtraArgs = "--workspace --exclude blit-browser";
          doCheck = false;
          preBuild = copyWebAppDist;
        }
        // pkgs.lib.optionalAttrs pkgs.stdenv.isLinux {
          postFixup = ''
            for bin in $out/bin/*; do
              if ! file "$bin" | grep -qE "static(ally|-pie) linked"; then
                echo "FATAL: $bin is not statically linked:"
                file "$bin"
                exit 1
              fi
            done
          '';
        }
        // pkgs.lib.optionalAttrs pkgs.stdenv.isDarwin {
          postFixup = ''
            for bin in $out/bin/*; do
              for lib in $(otool -L "$bin" | tail -n +2 | awk '/\/nix\/store\//{print $1}'); do
                base=$(basename "$lib")
                case "$base" in
                  libiconv.*|libiconv-*) sys="/usr/lib/libiconv.2.dylib" ;;
                  libz.*|libz-*) sys="/usr/lib/libz.1.dylib" ;;
                  libc++.*) sys="/usr/lib/libc++.1.dylib" ;;
                  libc++abi.*) sys="/usr/lib/libc++abi.dylib" ;;
                  libresolv.*) sys="/usr/lib/libresolv.9.dylib" ;;
                  libSystem.*) sys="/usr/lib/libSystem.B.dylib" ;;
                  *) echo "FATAL: unknown nix-store dylib: $lib"; exit 1 ;;
                esac
                echo "rewriting $lib -> $sys"
                install_name_tool -change "$lib" "$sys" "$bin"
              done
            done
          '';
        }
      );

      mkStaticBin =
        {
          pname,
          binName ? pname,
        }:
        pkgs.runCommand "${pname}-static-${version}" { } ''
          mkdir -p $out/bin
          cp ${blit-workspace-static}/bin/${binName} $out/bin/
        '';

      blit-server-static = mkStaticBin { pname = "blit-server"; };
      blit-cli-static = mkStaticBin {
        pname = "blit-cli";
        binName = "blit";
      };
      blit-gateway-static = mkStaticBin { pname = "blit-gateway"; };
      blit-proxy-static = mkStaticBin { pname = "blit-proxy"; };
      blit-webrtc-forwarder-static = mkStaticBin { pname = "blit-webrtc-forwarder"; };

      # ------------------------------------------------------------------
      # JS / Web assets
      # ------------------------------------------------------------------

      setupBrowserPkg = ''
        mkdir -p crates/browser/pkg/snippets
        cp ${browserWasm}/blit_browser.js crates/browser/pkg/
        cp ${browserWasm}/blit_browser_bg.wasm crates/browser/pkg/
        cp ${browserWasm}/blit_browser.d.ts crates/browser/pkg/
        cp ${browserWasm}/blit_browser_bg.wasm.d.ts crates/browser/pkg/
        echo '{"name":"@blit-sh/browser","version":"${version}","main":"blit_browser.js","types":"blit_browser.d.ts"}' > crates/browser/pkg/package.json
        for d in ${browserWasm}/snippets/blit-browser-*/; do
          name=$(basename "$d")
          mkdir -p "crates/browser/pkg/snippets/$name"
          cp "$d"/* "crates/browser/pkg/snippets/$name/"
        done
      '';

      pnpmDeps = pkgs.fetchPnpmDeps {
        pname = "blit-js";
        inherit version;
        src = ../.;
        fetcherVersion = 3;
        postPatch = setupBrowserPkg + ''
          cd js
        '';
        hash = "sha256-wyMe+IE6XcyVnTkvYi6tk+0xXLTlYnl7QEuagGS789k=";
      };

      webAppDist = pkgs.stdenv.mkDerivation {
        pname = "blit-ui";
        inherit version;
        src = ../.;
        inherit pnpmDeps;
        nativeBuildInputs = [
          pkgs.nodejs
          pkgs.pnpm
          pkgs.pnpmConfigHook
        ];
        pnpmRoot = "js";
        postPatch = setupBrowserPkg;
        buildPhase = ''
          cd js
          pnpm --filter @blit-sh/core run build
          pnpm --filter @blit-sh/solid run build
          pnpm --filter @blit-sh/ui run build
        '';
        installPhase = ''
          mkdir -p $out
          cp ui/dist/index.html ui/dist/index.html.br $out/
        '';
        doCheck = false;
      };

      websiteDist = pkgs.stdenv.mkDerivation {
        pname = "blit-website";
        inherit version;
        src = ../.;
        inherit pnpmDeps;
        nativeBuildInputs = [
          pkgs.nodejs
          pkgs.pnpm
          pkgs.pnpmConfigHook
        ];
        pnpmRoot = "js";
        postPatch = setupBrowserPkg;
        buildPhase = ''
          cd js
          pnpm --filter blit-website run build
        '';
        installPhase = ''
          mkdir -p $out
          cp -r website/dist/* $out/
        '';
        doCheck = false;
      };

      copyWebAppDist = ''
        mkdir -p js/ui/dist
        cp ${webAppDist}/index.html ${webAppDist}/index.html.br js/ui/dist/
      '';

      # Man pages and shell completions are generated by the blit-gen
      # binary (built as part of the workspace) during postInstall.

      # ------------------------------------------------------------------
      # Docker / tasks
      # ------------------------------------------------------------------

      tasks = import ./tasks.nix {
        inherit
          pkgs
          version
          browserWasm
          blit-server
          blit-gateway
          blit-proxy
          blit-server-static
          blit-cli-static
          blit-gateway-static
          blit-proxy-static
          blit-webrtc-forwarder-static
          webAppDist
          websiteDist
          rustToolchain
          ;
      };

      demoImage =
        let
          fishConfig = pkgs.writeTextDir "home/blit/.config/fish/config.fish" ''
            function fish_greeting
                cat /etc/blit-welcome 2>/dev/null
            end
          '';
          welcomeFile = pkgs.writeTextDir "etc/blit-welcome" (
            if builtins.pathExists ../welcome then builtins.readFile ../welcome else ""
          );
          passwd = pkgs.writeTextDir "etc/passwd" "blit:x:1000:1000:blit:/home/blit:/bin/fish\n";
          group = pkgs.writeTextDir "etc/group" "blit:x:1000:\n";
        in
        pkgs.dockerTools.buildLayeredImage {
          name = "grab/blit-demo";
          tag = "latest";
          maxLayers = 2;
          contents = [
            pkgs.dockerTools.caCertificates
            pkgs.dockerTools.binSh
            pkgs.busybox
            pkgs.fish
            pkgs.htop
            pkgs.neovim
            pkgs.git
            pkgs.curl
            pkgs.jq
            pkgs.tree
            pkgs.ncdu
            pkgs.mpv
            pkgs.imv
            pkgs.wayland-utils
            pkgs.foot
            pkgs.wev
            pkgs.zathura
            blit-cli
            fishConfig
            welcomeFile
            passwd
            group
          ];
          fakeRootCommands = ''
            mkdir -p ./home/blit ./tmp
            chown -R 1000:1000 ./home/blit
            chmod 1777 ./tmp
          '';
          config = {
            Env = [
              "SHELL=/bin/fish"
              "USER=blit"
              "HOME=/home/blit"
              "TERM=xterm-256color"
            ];
            User = "1000:1000";
            WorkingDir = "/home/blit";
            ExposedPorts = {
              "3264/tcp" = { };
            };
            Entrypoint = [
              "blit"
              "share"
            ];
          };
        };

      skopeoPolicy = pkgs.writeText "containers-policy.json" ''{"default":[{"type":"insecureAcceptAnything"}]}'';

      pushDemo = pkgs.writeShellApplication {
        name = "push-demo";
        runtimeInputs = [ pkgs.skopeo ];
        text = ''
          arch="''${1:?usage: push-demo <amd64|arm64> [version]}"
          version="''${2:-}"
          skopeo --policy ${skopeoPolicy} login docker.io -u "$DOCKERHUB_USERNAME" -p "$DOCKERHUB_TOKEN"
          skopeo --policy ${skopeoPolicy} copy "docker-archive:${demoImage}" "docker://docker.io/grab/blit-demo:latest-$arch"
          if [[ "$version" != "" ]]; then
            skopeo --policy ${skopeoPolicy} copy "docker-archive:${demoImage}" "docker://docker.io/grab/blit-demo:$version-$arch"
          fi
        '';
      };

      publishDemo = pkgs.writeShellApplication {
        name = "publish-demo";
        runtimeInputs = [ pkgs.crane ];
        text = ''
          version="''${1:-}"
          crane auth login docker.io -u "$DOCKERHUB_USERNAME" -p "$DOCKERHUB_TOKEN"
          crane index append \
            -t "docker.io/grab/blit-demo:latest" \
            -m "docker.io/grab/blit-demo:latest-amd64" \
            -m "docker.io/grab/blit-demo:latest-arm64"
          if [[ "$version" != "" ]]; then
            crane index append \
              -t "docker.io/grab/blit-demo:$version" \
              -m "docker.io/grab/blit-demo:$version-amd64" \
              -m "docker.io/grab/blit-demo:$version-arm64"
          fi
        '';
      };
    in
    {
      packages = {
        blit = blit-cli;
        inherit
          blit-server
          blit-cli
          blit-gateway
          blit-proxy
          blit-webrtc-forwarder
          ;
        inherit
          blit-server-static
          blit-cli-static
          blit-gateway-static
          blit-proxy-static
          blit-webrtc-forwarder-static
          ;
        demo-image = demoImage;
        push-demo = pushDemo;
        publish-demo = publishDemo;
        default = blit-cli;
      }
      // tasks;

      devShells.default = pkgs.mkShell {
        buildInputs = [
          rustToolchain
          pkgs.binaryen
          pkgs.bun
          pkgs.cargo-flamegraph
          pkgs.cargo-llvm-cov
          pkgs.cargo-edit
          pkgs.cargo-watch
          pkgs.curl
          pkgs.flyctl
          pkgs.libxkbcommon
          pkgs.nodejs
          pkgs.pixman
          pkgs.pkg-config
          pkgs.pkgsStatic.stdenv.cc
          pkgs.pnpm
          pkgs.process-compose
          pkgs.samply
          pkgs.wasm-bindgen-cli
          pkgs.wasm-pack
        ]
        ++ pkgs.lib.optionals serverVaapiEnabled [
          pkgs.ffmpeg-headless
          pkgs.libva
          pkgs.llvmPackages.libclang
        ];

        shellHook = ''
          if [ -z "''${LANG-}" ]; then
            export LANG="$(defaults read -g AppleLocale 2>/dev/null | sed 's/@.*//' || echo en_US).UTF-8"
          fi
          export BINDGEN_EXTRA_CLANG_ARGS="${bindgenClangArgs}''${NIX_CFLAGS_COMPILE:+ $NIX_CFLAGS_COMPILE}"
          export LIBCLANG_PATH="${pkgs.llvmPackages.libclang.lib}/lib"
          export PKG_CONFIG_PATH="${pkgs.libxkbcommon.dev}/lib/pkgconfig:${pkgs.pixman}/lib/pkgconfig${
            if serverVaapiEnabled then
              ":${pkgs.ffmpeg-headless.dev}/lib/pkgconfig:${pkgs.libva.dev}/lib/pkgconfig"
            else
              ""
          }''${PKG_CONFIG_PATH:+:$PKG_CONFIG_PATH}"
          export LIBRARY_PATH="${pkgs.libxkbcommon}/lib:${pkgs.pixman}/lib${
            if serverVaapiEnabled then ":${pkgs.ffmpeg-headless.lib}/lib:${pkgs.libva}/lib" else ""
          }''${LIBRARY_PATH:+:$LIBRARY_PATH}"
          # Runtime dlopen: blit-server loads VA-API / NVENC libraries at
          # runtime via dlopen.  See gpuRuntimeLibPath definition above.
          ${pkgs.lib.optionalString (
            gpuRuntimeLibPath != ""
          ) ''export LD_LIBRARY_PATH="${gpuRuntimeLibPath}''${LD_LIBRARY_PATH:+:$LD_LIBRARY_PATH}"''}
          export PATH="$PWD/bin:$PATH"
        '';
      };
    };
}
